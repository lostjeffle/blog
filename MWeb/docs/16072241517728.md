title:'Init - 4 Kernel Entry - x86'
## Init - 4 Kernel Entry - x86


系统初始化过程中，bootstrap loader 在完成相应的任务之后，会将控制权传递给 kernel， x86_64 架构下内核的入口函数为 startup_64


### entry

`arch/x86/kernel/head_64.S` 中定义 IA 64 架构下内核的入口函数为 startup_64

```asm
	.text
	__HEAD
	.code64
	.globl startup_64
startup_64:
	/*
	 * At this point the CPU runs in 64bit mode CS.L = 1 CS.D = 0,
	 * and someone has loaded an identity mapped page table
	 * for us.  These identity mapped page tables map all of the
	 * kernel pages and possibly all of memory.
	 *
	 * %rsi holds a physical pointer to real_mode_data.
	 *
	 * We come here either directly from a 64bit bootloader, or from
	 * arch/x86_64/boot/compressed/head.S.
	 *
	 * We only come here initially at boot nothing else comes here.
	 *
	 * Since we may be loaded at an address different from what we were
	 * compiled to run at we first fixup the physical addresses in our page
	 * tables and then reload them.
	 */
```


1. vmlinux base physical/virtual address

`arch/x86/kernel/vmlinux.lds.S` 中定义

```lda
SECTIONS
{
#ifdef CONFIG_X86_32
        ...
#else
        . = __START_KERNEL;
#endif

	/* Text and read-only data */
	.text :  AT(ADDR(.text) - LOAD_OFFSET) {...}
	...
}
```

即 linux kernel 的 base virtual address 为 __START_KERNEL


`arch/x86/include/asm/page_64_type.h` 中定义

__PHYSICAL_START 是内核的 base physical address，其值通常为 0x1000000

```c
#define __PHYSICAL_START	((CONFIG_PHYSICAL_START + \
				  (CONFIG_PHYSICAL_ALIGN - 1)) & \
				 ~(CONFIG_PHYSICAL_ALIGN - 1))
```


__START_KERNEL 表示内核的 base virtual address，其值通常为 0xffffffff81000000

```c
#define __START_KERNEL		(__START_KERNEL_map + __PHYSICAL_START)
```

__START_KERNEL_map 的值为 0xffffffff80000000

```c
#define __START_KERNEL_map	_AC(0xffffffff80000000, UL)
```


2. calculate delta between compile address and load address

```asm
	/*
	 * Compute the delta between the address I am compiled to run at and the
	 * address I am actually running at.
	 */
	leaq	_text(%rip), %rbp
	subq	$_text - __START_KERNEL_map, %rbp
```

rbp 寄存器中保存 vmlinux 的 compile address 与实际的 load address 之间的差值


3. fixup page table

程序中静态定义

```asm
#define NEXT_PAGE(name) .balign PAGE_SIZE; GLOBAL(name)

	__INITDATA
NEXT_PAGE(early_level4_pgt)
	.fill	511,8,0
	.quad	level3_kernel_pgt - __START_KERNEL_map + _PAGE_TABLE

NEXT_PAGE(early_dynamic_pgts)
	.fill	512*EARLY_DYNAMIC_PAGE_TABLES,8,0

	.data
NEXT_PAGE(init_level4_pgt)
	.fill	512,8,0

NEXT_PAGE(level3_kernel_pgt)
	.fill	L3_START_KERNEL,8,0
	/* (2^48-(2*1024*1024*1024)-((2^39)*511))/(2^30) = 510 */
	.quad	level2_kernel_pgt - __START_KERNEL_map + _KERNPG_TABLE
	.quad	level2_fixmap_pgt - __START_KERNEL_map + _PAGE_TABLE

NEXT_PAGE(level2_kernel_pgt)
	/*
	 * 512 MB kernel mapping. We spend a full page on this pagetable
	 * anyway.
	 *
	 * The kernel code+data+bss must not be bigger than that.
	 *
	 * (NOTE: at +512MB starts the module area, see MODULES_VADDR.
	 *  If you want to increase this then increase MODULES_VADDR
	 *  too.)
	 */
	PMDS(0, __PAGE_KERNEL_LARGE_EXEC,
		KERNEL_IMAGE_SIZE/PMD_SIZE)NEXT_PAGE(level2_fixmap_pgt)
	.fill	506,8,0
	.quad	level1_fixmap_pgt - __START_KERNEL_map + _PAGE_TABLE
	/* 8MB reserved for vsyscalls + a 2MB hole = 4 + 1 entries */
	.fill	5,8,0

NEXT_PAGE(level1_fixmap_pgt)
	.fill	512,8,0
```

- 一个 level 4 page table
    - 将之前的 511 个 entry 清为 0
    - 最后的一个 entry 指向 level3_kernel_pgt

- 一个 level 3 page table
    - 将之前的 510 个 entry 清为 0
    - 倒数第二个 entry 指向 level2_kernel_pgt
    - 倒数第一个 entry 指向 level2_fixmap_pgt


存在两个 level 2 page table

- level2_kernel_pgt
    - 前 256 个 entry 实现 512MB 的 kernel direct mapping 的映射

- level2_fixmap_pgt
    - entry[506] 指向 level1_fixmap_pgt
    - 其余 entry 清为 0

- level1_fixmap_pgt
    - 所有 512 个 entry 清为 0


```asm
	/*
	 * ç in the page table
	 */
	addq	%rbp, early_level4_pgt + (L4_START_KERNEL*8)(%rip)

	addq	%rbp, level3_kernel_pgt + (510*8)(%rip)
	addq	%rbp, level3_kernel_pgt + (511*8)(%rip)

	addq	%rbp, level2_fixmap_pgt + (506*8)(%rip)
```

rbp 寄存器中保存 compile addres 与 load address 的差值，此时将 page table 中各个 entry 保存的 physical address 加上该差值，从而使得这些 entry 中保存其映射的目标真正的 physical load address


4. identity mapping

```asm
	/*
	 * Set up the identity mapping for the switchover.  These
	 * entries should *NOT* have the global bit set!  This also
	 * creates a bunch of nonsense entries but that is fine --
	 * it avoids problems around wraparound.
	 */
	leaq	_text(%rip), %rdi
	leaq	early_level4_pgt(%rip), %rbx

	movq	%rdi, %rax
	shrq	$PGDIR_SHIFT, %rax

	leaq	(4096 + _KERNPG_TABLE)(%rbx), %rdx
	movq	%rdx, 0(%rbx,%rax,8)
	movq	%rdx, 8(%rbx,%rax,8)

	addq	$4096, %rdx
	movq	%rdi, %rax
	shrq	$PUD_SHIFT, %rax
	andl	$(PTRS_PER_PUD-1), %eax
	movq	%rdx, 4096(%rbx,%rax,8)
	incl	%eax
	andl	$(PTRS_PER_PUD-1), %eax
	movq	%rdx, 4096(%rbx,%rax,8)

	addq	$8192, %rbx
	movq	%rdi, %rax
	shrq	$PMD_SHIFT, %rdi
	addq	$(__PAGE_KERNEL_LARGE_EXEC & ~_PAGE_GLOBAL), %rax
	leaq	(_end - 1)(%rip), %rcx
	shrq	$PMD_SHIFT, %rcx
	subq	%rdi, %rcx
	incl	%ecx
```


5. enable paging

```asm
1:

	/* Enable PAE mode and PGE */
	movl	$(X86_CR4_PAE | X86_CR4_PGE), %ecx
	movq	%rcx, %cr4

	/* Setup early boot stage 4 level pagetables. */
	addq	phys_base(%rip), %rax
	movq	%rax, %cr3
```

- 设置 CR4 的 PAE、PGE (paging global extension)，以开启 paging 机制
- 将 level 4 page table 的 base physical address 保存到 CR3 寄存器中


6. jump to C

```asm
	/* Finally jump to run C code and to be on real kernel address
	 * Since we are running on identity-mapped space we have to jump
	 * to the full 64bit address, this is only possible as indirect
	 * jump.  In addition we need to ensure %cs is set so we make this
	 * a far return.
	 *
	 * Note: do not change to far jump indirect with 64bit offset.
	 *
	 * AMD does not support far jump indirect with 64bit offset.
	 * AMD64 Architecture Programmer's Manual, Volume 3: states only
	 *	JMP FAR mem16:16 FF /5 Far jump indirect,
	 *		with the target specified by a far pointer in memory.
	 *	JMP FAR mem16:32 FF /5 Far jump indirect,
	 *		with the target specified by a far pointer in memory.
	 *
	 * Intel64 does support 64bit offset.
	 * Software Developer Manual Vol 2: states:
	 *	FF /5 JMP m16:16 Jump far, absolute indirect,
	 *		address given in m16:16
	 *	FF /5 JMP m16:32 Jump far, absolute indirect,
	 *		address given in m16:32.
	 *	REX.W + FF /5 JMP m16:64 Jump far, absolute indirect,
	 *		address given in m16:64.
	 */
	movq	initial_code(%rip),%rax
	pushq	$0		# fake return address to stop unwinder
	pushq	$__KERNEL_CS	# set correct cs
	pushq	%rax		# target address in negative space
	lretq
```

最终跳转执行 initial_code


```asm
	GLOBAL(initial_code)
	.quad	x86_64_start_kernel
```

initial_code 实际为 x86_64_start_kernel，x86_64_start_kernel() 中最终会调用 start_kernel()
