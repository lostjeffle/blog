title:'Init - 4 Kernel Entry - ARM'
## Init - 4 Kernel Entry - ARM

内核映像的 head.o 执行架构和处理器相关的初始化工作，`arch/arm/kernel/head.S` 定义 arm32 架构下内核映像的 head.o


### entry point

`arch/arm/kernel/vmlinux.lds.S` 中

```
ENTRY(stext)
```

kernel image 的 entry point 为 stext label


1. disable IRQ/FIQ interrupt

`arch/arm/kernel/head.S` 中定义 stext label

```s
	.arm

	__HEAD
ENTRY(stext)
#ifdef CONFIG_ARM_VIRT_EXT
	bl	__hyp_stub_install
#endif
	@ ensure svc mode and all interrupts masked
	safe_svcmode_maskall r9
```

调用 safe_svcmode_maskall 宏使处理器进入 SVC mode，同时 disable IRQ/FIQ interrupt


2. save MIDR(Main ID Register)

```s
	mrc	p15, 0, r9, c0, c0	@ get processor id
	bl	__lookup_processor_type @ r5=procinfo r9=cpuid
```

读取 MIDR(Main ID Register) 寄存器的值并保存到 r9 中，之后跳转到 __lookup_processor_type label 处寻找对应的处理器类型


3. __lookup_processor_type

```s
__lookup_processor_type:
	adr	r3, __lookup_processor_type_data
	ldmia	r3, {r4 - r6}
	sub	r3, r3, r4			@ get offset between virt&phys
	add	r5, r5, r3			@ convert virt addresses to
	add	r6, r6, r3			@ physical address space
1:	ldmia	r5, {r3, r4}			@ value, mask
	and	r4, r4, r9			@ mask wanted bits
	teq	r3, r4
	beq	2f
	add	r5, r5, #PROC_INFO_SZ		@ sizeof(proc_info_list)
	cmp	r5, r6
	blo	1b
	mov	r5, #0				@ unknown processor
2:	mov	pc, lr
ENDPROC(__lookup_processor_type)
```

循环遍历 proc_info 列表，查找目标处理器；当 R3 = R4 & R9 时，说明找到了，则跳出循环，R5 是“指向目标处理器的 proc_info 结构体的指针”； 若遍历完之后还没找到，则 R5 = 0，即

- r3, r4, r6 被修改
- r5 = 指向找到的 proc_info 结构体 或 为 0（没找到）
- r9 = cpuid (= MIDR)，未被修改


4. call init function

```s
	movs	r10, r5				@ invalid processor (r5=0)?
 THUMB(it	eq)		@ force fixup-able long branch encoding
	beq	__error_p			@ yes, error 'p'
```

__lookup_processor_type 执行完毕后，回到 stext 处，此时检查若 __lookup_processor_type 中没有找到当前运行的处理器对应的 proc_info，则跳转到 __error_p 进入无限循环，否则继续向下执行


```s
 ARM(add	pc, r10, #PROCINFO_INITFUNC) @ initialise processor
```

之前在 r10 中存储对应处理器的 proc_info 结构体的指针，此时更新 PC 寄存器的值，使 CPU 跳转执行相应的初始化函数，即 struct proc_info_list 的 __cpu_flush 字段指向的初始化函数


### init processor

例如 ARMv7 架构下处理器的初始化函数为 __v7_ca9mp_setup

1. calculate physical address of kernel image

```s
#ifndef CONFIG_XIP_KERNEL
	adr	r3, 2f
	ldmia	r3, {r4, r8}
	sub	r4, r3, r4			@ (PHYS_OFFSET - PAGE_OFFSET)
	add	r8, r8, r4			@ PHYS_OFFSET
#else
	ldr	r8, =PHYS_OFFSET		@ always constant in this case
#endif

...

#ifndef CONFIG_XIP_KERNEL
2:	.long	.
	.long	PAGE_OFFSET
#endif
```

- r3 中保存 label 2 的 running address 即 physical address
- r4 中保存 label 2 的 link address 即 virtual address
- 因而 (r3 - r4) 即为 link virtual address 与实际的 running physical address 之间的偏差，即 (PHYS_OFFSET - PAGE_OFFSET)，将该偏差保存在 r4 中

在 r8 中保存 PAGE_OFFSET，PAGE_OFFSET 描述 kernel image 的 base virtual address，使其加上 link virtual address 到 running physical address 的偏差，即为 kernel image 的 base physical address


2. validate paramters from bootloader

```s
bl	__vet_atags
```

跳转执行 __vet_atags 验证 Bootloader 传入的内核参数列表是否合法


3. __create_page_tables

```s
bl	__create_page_tables 
```

跳转执行 __create_page_tables 创建页表


4. __enable_mmu

```s
b	__enable_mmu
```

开启 MMU


5. __mmap_switched

__enable_mmu 中开启 MMU 之后会跳转执行 __mmap_switched

```s
/*
 * The following fragment of code is executed with the MMU on in MMU mode,
 * and uses absolute addresses; this is not position independent.
 *
 *  r0  = cp#15 control register
 *  r1  = machine ID
 *  r2  = atags/dtb pointer
 *  r9  = processor ID
 */
	__INIT
__mmap_switched:
	adr	r3, __mmap_switched_data

	ldmia	r3!, {r4, r5, r6, r7}
	cmp	r4, r5				@ Copy data segment if needed
1:	cmpne	r5, r6
	ldrne	fp, [r4], #4
	strne	fp, [r5], #4
	bne	1b

	mov	fp, #0				@ Clear BSS (and zero fp)
1:	cmp	r6, r7
	strcc	fp, [r6],#4
	bcc	1b

 ARM(ldmia	r3, {r4, r5, r6, r7, sp})
 THUMB(ldmia	r3, {r4, r5, r6, r7})
 THUMB(ldr	sp, [r3, #16]	)str	r9, [r4]			@ Save processor ID
	str	r1, [r5]			@ Save machine type
	str	r2, [r6]			@ Save atags pointer
	cmp	r7, #0
	bicne	r4, r0, #CR_A			@ Clear 'A' bit
	stmneia	r7, {r0, r4}		@ Save control register values
	b	start_kernel
ENDPROC(__mmap_switched)
```


> 5.1 setup SP register

```s
ARM(ldmia	r3, {r4, r5, r6, r7, sp})
...
.long	init_thread_union + THREAD_START_SP @ sp 
```

设置 SP = R13 = init_thread_union + THREAD_START_SP

```c
#define THREAD_START_SP		(THREAD_SIZE - 8)
```

`init/init_task.c` 中静态定义一个 kernel stack，大小为 8192 字节，即 2 个 page frame

```c
/*
 * Initial thread structure. Alignment of this is handled by a special
 * linker map entry.
 */
union thread_union init_thread_union __init_task_data =
	{INIT_THREAD_INFO(init_task) };
```

```c
union thread_union {
	struct thread_info thread_info;
	unsigned long stack[THREAD_SIZE/sizeof(long)];
};
```

即此时将 SVC mode 下的 SP 寄存器的值设置为该静态定义的 kernel stack 的栈底，由于 arm 中栈向下增长，因而 (stack base address + stack size) 即为栈底地址，之后 SVC mode 下就将使用这一块内存作为 kernel stack


> 5.2 call start_kernel

```
    b	start_kernel
```

最终跳转执行内核映像 main.o 的 start_kernel()

